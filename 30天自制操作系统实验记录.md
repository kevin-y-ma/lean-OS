# qemu安装以及简单使用

```shell
$ sudo apt update 
$ sudo apt install qemu

# 创建磁盘
qemu-img create  ubuntu.img 10G
# 安装系统到磁盘
qemu -hda ubuntu.img -boot d -cdrom ubuntu.iso -m 512 # -boot [a|d|c]选项让qemu从a:磁盘，d:光盘 c:硬盘启动
#  'drives': floppy (a), hard disk (c), CD-ROM (d), network (n)
# 安装完成后启动虚拟机
qemu ubuntu.img -m 512

```

# Bochs

安装

```shell
$ sudo apt install bochs  bochs-x
```

配置并运行

```shell 
$ vim bochsrc.bxrc
# 写入
floppya: 1_44="./Image", status=inserted
boot: floppy
# 运行
$ bochs -f bochsrc.bxrc -q
```

使用

b : 设置断点 例： b 0x7c00

c : continue 继续运行，到断点位置停下

r: register 显示通用寄存器的内容

s： step 单步执行

sreg: segment register 显示段寄存器的内容

xp: eXamine memory at Physical address 显示指定物理内存地址处的内容，xp命令每次只显示一个双字，可以用/附加一个数量，例： xp/2 0xb8000

q: quit退出

n: 自动完成循环过程

u: 反汇编指令，例： u/2  反汇编两条指令

info eflags  ：显示标志位

print-stack : 查看栈的信息

creg: 查看控制寄存器的内容

# 30天自制操作系统



##  day 1-3



###  hello world

```shell 
$ vim ipl.s
```

写入

```assembly
; helloOS
; 显示hello world的程序
ORG	0X7C00
; 以下的记述用于标准FAT12格式的软盘
start:
    JMP    entry
    DB    "HELLOIPL"    ; 启动区名称(8字节)
    DW    512        ; 扇区大小(512字节)
    DB    1        ; 簇大小(1扇区)
    DW    1        ; FAT起始位置
    DB    2        ; FAT个数
    DW    224        ; 根目录大小(224项)
    DW    2880        ; 磁盘大小(2880扇区)
    DB    0xf0        ; 磁盘种类
    DW    9        ; FAT长度
    DW    18        ; 每个磁道扇区数
    DW    2        ; 磁头数
    DD    0        ; 不使用分区
    DD    2880        ; 重写一次磁盘大小
    DB    0,0,0x29    ; 意义不明
    DD    0xffffffff    ; 可能是卷标号码
    DB    "HELLO-OS   "    ; 磁盘名称(11字节)
    DB    "FAT12   "    ; 格式名称(8字节)
    RESB    18        ; 空出18字节

entry:
	MOV AX, 0
	MOV SS, AX
	MOV SP, 0x7c00
	MOV DS, AX
	MOV ES, AX
	
	MOV SI, msg

putloop:
	MOV AL, [SI]
	ADD SI, 1
	CMP AL, 0

	JE fin		; if (AL == 0) { goto fin }
	MOV AH, 0x0e	;显示一个文字
	MOV BX, 15	;指定字符颜色
	INT 0x10 	;调用显卡BIOS	
	JMP putloop
fin:
	HLT
	JMP fin

msg:
	DB 0x0a, 0x0a
	DB "hello world"
	DB 0x0a
	DB 0

tail:
	RESB 	0x1fe-(tail-start)
	DB 	0x55, 0xaa
```

使用nasm编译

```shell
$ nasm -f bin ipl.s -o ipl.bin 

  -f format
           Specifies the output file format. To see a list of valid output formats, use the -hf option.
           
$ file ipl.bin
			ipl.bin: DOS/MBR boot sector
			
$ qemu-img info ipl.bin
            image: hello.bin
            file format: raw
            virtual size: 512 B (512 bytes)
            disk size: 4 KiB
```

启动虚拟机

```shell
$ qemu-system-i386 -hda ipl.bin -boot a 
```

### 磁盘操作

```assembly
AH=0x02		;	读盘
AH=0x03		; 	写盘
AH=0x04		; 	校验
AH=0x0c		;   寻道

AL=处理对象的扇区数目
CH=柱面号 & 0xff
CL=扇区号（0-5位）| （柱面号&0x300）>>2
DH=磁头号
DL=驱动器号
ES:BX=缓冲地址

返回值：
FLAGS.CF==0 ;没有错误 AH=0
FLAGS.CF==1 ;有错误 AH内存储错误码
```

第一个扇区为启动分区系统启动后自动加载，其他扇区需要第一个分区中的代码加载到内存中的指定位置。

C0-H0-S1(柱面0-磁头0-扇区1) ---> C0-H0-S2 ---> ... --->C0-H0-S18 ---> C0-H1-S1 ---> ... ---> C1-H0-S1

 

```shell
$ vim ipl.S 
```

写入以下内容，第一个扇区为启动扇区，以0x55aa结尾，该扇区要读取后续扇区的内容到内存中。

```assembly
;ipl
	ORG	0x07c00 	;设置程序加载地址
entry:
;设置堆栈
	MOV 	AX, 0
	MOV 	SS, AX
	MOV 	SP, 0x7c00
;设置数据段
	MOV 	DS, AX 

;显示提示信息
welcome:
	MOV 	SI, msg_start  
startloop: 
	MOV 	AL, [SI]
	ADD 	SI, 1
	CMP 	AL, 0
	JE 	read 
	
	MOV 	AH, 0x0e	;设置显示模式
	MOV 	BX, 0x0b	;设置字体颜色为白色
	INT 	0x10		;调用BIOS显示字符
	JMP 	startloop 

;读磁盘
read:
	CYLS EQU 10	;计划读取10个柱面
	MOV	AX, 0x0c20
	MOV	ES, AX
	MOV	CH, 0	;柱面0
	MOV	DH, 0	;磁头0
	MOV 	CL, 2	;扇区2
readloop:
	MOV 	SI, 0	;记录失败次数

retry:
	MOV 	AH, 0x02 	;读取磁盘
	MOV 	AL, 1		;1个扇区
	MOV 	BX, 0		;ES:BX缓冲地址
	MOV 	DL, 0X00	;A驱动器
	INT	0x13		;启动BIOS读取磁盘
	
	JNC	next		;FLAGS.CF==0 没有错误
	ADD 	SI, 1
	CMP	SI, 5
	JAE	error		; SI>5 错误满5次
	
	MOV 	AH, 0X00
	MOV 	DL, 0X00
	INT 	0x13 		;重置驱动器
	JMP	retry

next:
	MOV 	AX, ES 		;将缓冲区地址往后挪一个扇区
	ADD 	AX, 0x0020
	MOV 	ES, AX
	ADD 	CL, 1 		;读取下一个扇区
	CMP 	CL, 18
	JBE	readloop 	;if (CL<18) 继续读取下一个扇区
	MOV 	CL, 1		;第一个磁头读取完了读取下一个磁头
	ADD 	DH, 1
	CMP 	DH, 2
	JB 	readloop 	;if (DH<2) 
	MOV 	DH, 0
	ADD 	CH, 1
	CMP  	CH, CYLS 
	JB 	readloop 	;if (CH<CYLS)

;跳转到新地址运行
	;MOV 	[0x0ff0], CH
	JMP 	0xc200

;显示出错信息
error:
	MOV 	SI, msg 
putloop:
	MOV 	AL, [SI]
	ADD 	SI, 1
	CMP 	AL, 0
	JE 	fin
	
	MOV 	AH, 0x0e	;设置显示模式
	MOV 	BX, 15		;设置字体颜色为白色
	INT 	0x10		;调用BIOS显示字符
	JMP 	putloop 

;停机
fin:
	HLT
	JMP fin 

msg:
	DB	0x0a, 0x0a
	DB 	"Load error"
	DB 	0x0a 		;换行
	DB 	0
msg_start:
	DB	0x0a, 0x0a
	DB 	"Load dome start..."
	DB 	0x0a 		;换行
	DB 	0


;在第一个扇区剩下的空间中填充0	
tail:
	RESB	0x1fe-(tail-entry)
	DB 	0x55, 0xaa
```

准备后续代码

```shell 
$ vim asmhead.s
```

写入以下内容，主要显示加载成功

```assembly
;Bootsect第二阶段
	ORG	0xC200
showdemo:
	;MOV 	AX, 0x0 
	;MOV 	SS, AX
	;MOV 	SP, 0xC200 
	;MOV 	DS, AX
	;MOV 	ES, AX

	MOV 	SI, msg

putloop:
	MOV 	AL, [SI]
	ADD	SI, 1
	CMP 	AL, 0

	JE fin
	MOV 	AH, 0x0e
	MOV 	BX, 0x0a
	INT 	0x10
	JMP 	putloop 

fin:
	HLT
	JMP 	fin 

msg:
	DB	0x0a, 0x0a
	DB 	"load successful"
	DB 	0x0a
	DB 	0
```

准备Makefile

```makefile
all: Image
	qemu-system-i386 -fda Image -boot a 	

bootsect.bin: ipl.s
	nasm -f bin ipl.s -o bootsect.bin
asmhead.bin: asmhead.s
	nasm -f bin asmhead.s -o asmhead.bin 

Image:	bootsect.bin asmhead.bin
	dd if=bootsect.bin of=Image bs=512 count=1
	dd if=asmhead.bin  of=Image bs=512 count=1 seek=1

clean:
	rm -rf *.bin Image
```





### 进入32位模式



### 导入C语言

